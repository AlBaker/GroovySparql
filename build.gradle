apply plugin:"groovy"
//apply plugin:"eclipse"
apply plugin:"maven"
apply plugin: 'signing'
//apply plugin: 'codenarc'

group = 'com.github.albaker'
version = '0.9.0'

repositories {
	mavenLocal()
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.3.8"

	compile 'org.apache.jena:jena:2.12.0'
	compile 'org.apache.jena:jena-arq:2.12.0'
	compile 'com.github.jsonld-java:jsonld-java:0.5.0'
	// this can be replaced after jena 2.12.2 or later
	// it was partially merged in 2.12.1, but not fully so it's incompatible
	// with 2.12.0
    compile 'org.apache.jena:jena-csv:1.0.0'
	compile 'org.codehaus.gpars:gpars:1.1.0'

	testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

compileGroovy{ 
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
	options.bootClasspath = "/usr/lib/jvm/jdk1.6.0_31/jre/lib/rt.jar"
}

task sourceJar(type: Jar) {
	description 'An archive of the source code for Maven Central'
	classifier 'sources'
	from sourceSets.main.groovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}


artifacts {
	archives javadocJar, sourceJar
}

signing {
	sign configurations.archives
	required { gradle.taskGraph.hasTask(uploadArchives) }
}

configure(install.repositories.mavenInstaller) {
	pom.project {
		version '0.8.1'
		artifactId 'GroovySparql'
		groupId 'com.github.albaker'
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			  authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
			   name 'GroovySparql'
			   packaging 'jar'
			   description 'Groovy SPARQL provides an easy to use Groovy API for W3C standard SPARQL endpoints'
			   url 'https://github.com/AlBaker/GroovySparql'

			   scm {
				   url 'scm:git://github.com/AlBaker/GroovySparql.git'
				   connection 'scm:git://github.com/AlBaker/GroovySparql.git'
				   developerConnection 'scm:git://github.com/AlBaker/GroovySparql.git'
			   }

			   licenses {
				   license {
					   name 'The Apache Software License, Version 2.0'
					   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					   distribution 'repo'
				   }
			   }

			   developers {
				   developer {
					   id 'albaker'
					   name 'Al Baker'
				   }
			   }
		   }
		}
	}
}
